#!/usr/bin/env perl
package phi;

use strict;
use warnings;

no warnings 'precedence';

use Carp;
use Data::Dumper;

$Data::Dumper::Indent = 0;

use phi::compiler;
use phi::struct ();
use editor ();

BEGIN
{
  $SIG{__DIE__} = sub { system 'stty sane'; print Carp::longmess(@_);
                        system 'reset' };
}


# Editor setup
use constant e => phi::editor::buffer->new(<<'EOF');
# phi grammar test?
x = 5;
y = x.plus(10).times(2);
y.plus(y)
EOF

use constant c => phi::editor::cursor->new(e);


sub phi::struct::int::eval
{
  my $self = shift;
  my ($x, $y) = @{$$self{args}};
  return $x      if $$self{op} eq 'const';
  return $x->eval + $y->eval if $$self{op} eq 'plus';
  return $x->eval - $y->eval if $$self{op} eq 'minus';
  return $x->eval * $y->eval if $$self{op} eq 'times';
  die "unimplemented op $$self{op}";
}


use Time::HiRes qw/time/;
use JSON;
use constant je => JSON->new->allow_nonref(1)->convert_blessed(1);

use constant root_scope => phi::compiler::nop_scope->new(
  undef,
  undef,
  qw/ phi::struct::double
      phi::struct::int
      phi::struct::string
      phi::struct::unknown
      phi::struct::assignment /);

use List::Util qw/min/;

system 'stty raw; stty -echo isig -ignbrk -brkint -ixon -ixoff';

my $ibuf = '';
while (read STDIN, $ibuf, 1, length $ibuf)
{
  $ibuf = substr $ibuf, 1 if $ibuf =~ /^\033/ && length $ibuf > 4;

  my $orig_pos = c->pos;

  c->col(0)  while $ibuf =~ s/^\033\[7~//;
  c->col(-1) while $ibuf =~ s/^\033\[8~//;
  c->move($1 eq 'A' ? -1 : $1 eq 'B' ? 1 : 0,
          $1 eq 'D' ? -1 : $1 eq 'C' ? 1 : 0)
    while $ibuf =~ s/^\033\[([ABCD])//;

  c->backspace(c->col || 1) while $ibuf =~ s/^\x15//;
  c->backspace              while $ibuf =~ s/^\x7f//;
  c->delete                 while $ibuf =~ s/^\033\[3~//;

  c->insert("  ") while $ibuf =~ s/^\t//;
  c->insert("\n") while $ibuf =~ s/^\r//;
  c->insert($1)   while $ibuf =~ s/^([^\033\x7f\x15]+)//;

  my $pos         = c->pos;
  my ($row, $col) = c->rowcol;

  print "\033[H\033[J";

  for my $l (0..e->lines + 2)
  {
    my $r = $l + 1;
    print "\033[$r;1H\033[K\033[0;0m" . (e->line($l) // '');
  }

  my $last_r  = e->lines + 1;
  my $error   = undef;
  my $plength = 0;
  my $explain =
    eval
    {
      local $SIG{__DIE__} = sub {
        print STDERR Carp::longmess(@_) =~ s/\n/\r\n/gr;
      };
      my ($ok, $l, @xs) = phi::compiler::block->new(root_scope)->parse(e, 0);

      $plength = $l;

      #Dumper("ok = $ok", @xs) =~ s/\n/\r\n/gr;
      $xs[-1]->eval;
    } // $@;

  {
    no warnings 'uninitialized';
    print "\033[$last_r;1H\033[K\033[0;37m$plength -> $explain";
    ++$last_r;
    #print "\033[$last_r;1H\033[K\033[0;37m" . Dumper($error);
  }

  my ($r, $c) = c->rowcol;
  $r++;
  $c++;
  print "\033[$r;${c}H";
}

system 'reset';
